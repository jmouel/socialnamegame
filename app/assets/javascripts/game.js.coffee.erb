jQuery(document).ready ($) ->
  tick = =>
    if not responded
      roundTime--
      if roundTime < 1
        checkAnswer(true)
      else
        $('.round').text(roundTime)
        setTimeout tick, 1000

  if document.location.pathname == '/game/begin_round'
    roundTime = 10
    responded = false
    imagePath = '<%= image_path "" %>'
    taunt = Math.ceil(Math.random()*8)
    window.setTimeout tick, 1000

  $('.btn[data-action="select-photo"]').click (e) =>
    target = $(e.target).attr('src')
    if not target?
      target = $(e.target).find('img').attr('src')

    @selectedPhoto = target
    $(e.target).closest('.photos').addClass('selected')
    checkAnswer()

  $('.btn[data-action="select-name"]').click (e) =>
    @selectedId = $(e.target).closest('button').data()['id']
    $(e.target).closest('.names').addClass('selected')
    checkAnswer()

  nextRound = =>
    $('.business-cat').removeClass('hidden')
    $('.info').html('Game continues in <span class="timeout">3</span> seconds')
    roundTime = 3
    setTimeout updateNextRoundTime, 1000

  updateNextRoundTime = =>
    roundTime--
    if roundTime < 1
      document.location.reload()
    else
      $('.timeout').text(roundTime)
      setTimeout updateNextRoundTime, 1000

  checkAnswer = (timeout = false) =>
    if (@selectedPhoto and @selectedId) or timeout
      responded = true
      $('.btn[data-action="select-photo"]').unbind()
      $('.btn[data-action="select-name"]').unbind()

      $.ajax
        url: 'eval_answer'
        type: 'post'
        headers:
          'X-CSRF-Token': $('meta[name="csrf-token"]').attr('content')
        data:
          id: @selectedId
          photo: @selectedPhoto

        success: (response) =>
          if response.correct
            $('.status').addClass('correct-status')
            $('.result').text('Correct!')

            $('.taunt').attr('src', "#{imagePath}/right#{taunt}.jpg")
            $('.business-cat').removeClass('hidden')
          else
            wrong(response)

          nextRound()

  wrong = (response) =>
    $('.status').addClass('incorrect-status')
    $('.result').text('Incorrect!')

    $('.taunt').attr('src', "#{imagePath}/wrong#{taunt}.jpg")
    $('.business-cat').removeClass('hidden')

    $('button').removeClass('active')
    if response
      answer = response.answer
      $('.btn[data-action="select-name"]').each (idx, name) =>
        if $(name).data()['id'] == answer['id']
          $(name).addClass('correct')

      $('.btn[data-action="select-photo"]').each (idx, photo) =>
        if $(photo).find('img').attr('src') == answer['photo_url']
          $(photo).addClass('correct')

